configfile: "config/config.yaml"


rule all:
    input:
        expand("results/rscripts/{id}_sex2embryo.RData", id=config["ID"]),


# In case of multiple libraries, combine aggregated data
rule combine_aggregated_data:
    input:
        expand(
            "results/snp/{id}_{sample}_allSNPcount.tsv",
            id=config["ID"],
            sample=config["Samples"],
        ),
    output:
        "results/snp/{id}_SNPcount.tsv",
    log:
        "logs/{id}_combine_aggregated_data.log",
    benchmark:
        "benchmarks/{id}_combine_aggregated_data.txt"
    shell:
        """
        awk '{{printf $1 "-" ARGIND "\\t" $2 "\\t" $3 "\\n" }}' {input} > {output} 2> {log}
        """


# Deconvolute single cells and assign to embryos
rule deconvolute_aggregated_data:
    input:
        "results/snp/{id}_SNPcount.tsv",
    output:
        pdf="results/rscripts/{id}_cell2embryo.pdf",
        cluster_rdata="results/rscripts/{id}_cell2embryo.RData",
        snp_profile="results/rscripts/{id}_cell2embryo_SNPprofile.pdf",
        session_rdata="results/rscripts/{id}_cell2embryo_session.RData",
    params:
        identifier=config["ID"],
        embryo_nr=config["embryo_nr"],
    log:
        "logs/{id}_deconvolute_aggregated_data.log",
    benchmark:
        "benchmarks/{id}_deconvolute_aggregated_data.txt"
    conda:
        "envs/rscripts.yaml"
    script:
        "scripts/cell2embryo.R"


# Determine sex of embryo
rule determine_sex:
    input:
        "results/rscripts/{id}_cell2embryo.RData",
    output:
        "results/rscripts/{id}_sex2embryo.RData",
    params:
        matrix_dir=config["matrix_dir_path"],
    log:
        "logs/{id}_determine_sex.log",
    benchmark:
        "benchmarks/{id}_determine_sex.txt"
    conda:
        "envs/rscripts.yaml"
    script:
        "scripts/sex2embryo_v3.R"


include: "rules/SNP_based_cell_to_embryo_assignment.smk"
include: "rules/prepare_alignment_for_deconvolution.smk"
